$${
IF(subs);
  LOG("&4[Macro] &cPesca do %PLAYER% &4DESLIGADO&c.");
  UNSET(subs);
  STOP;
ELSE;
  PICK(346);
  IF(%ITEM%!=346);
    LOG("&4[Macro] &cEsqueceu sua vara de pescar &4!!!");
    LOG("&4[Macro] &cPesca do %PLAYER% &4DESLIGADO&c.");
    STOP;
  ENDIF;    
  LOG("&2[Macro] &aPesca do %PLAYER% &2LIGADO&a.");
  #xpescar=%XPOS%;#zpescar=%ZPOS%;#ypescar=%YPOS%;
  SET(subs);
  SET(baus);
ENDIF;
 
&guardar="^264$|^265$|^266$|^276$|^277$|^278$|^279$|^310$|^311$|^312$|^313$|^349|^351|^369$|^370$|^2256$|^2257$|^2258$|^2259$|^2260$|^2261$|^2262$|^2263$|^2264$|^2265$|^2266$|^2267$";
DO;GUI(INVENTORY);WAIT(1);UNTIL(%GUI%="GUIINVENTORY");
LOOK(,0);LOOK(,-90);
DO;
  IF(baus);
    GETSLOTITEM(35,#id,#stack);
    IF(%#stack%!=0);
      LOOK(,0);
      WAIT(1);
      #vazio=0;
      FOR(#i,9,44);
        GETSLOTITEM(%#i%,#id,#stack);
        IFMATCHES(%#id%,%&guardar%|^346$|^287$);
        ELSE;
          SLOTCLICK(%#i%);
          SLOTCLICK(-999);
          IF(%#i%!=35);#vazio=%#i%;ENDIF;
          WAIT(80ms);
        ENDIF;
      NEXT;
      GETSLOTITEM(35,#id,#stack);
      IF(%#stack%!=0);
        IF(%#vazio%!=0);
          SLOTCLICK(35);SLOTCLICK(%#vazio%);
        ELSE;
          LOG("&2[Macro] &aInventário cheio, partiu guardar nos baús &2!!!");
          }$$/home guardar$${
          DO;UNTIL((%HITID%=54)||(%HITID%=146));
          #start=54;
          DO;
            DO;KEY(USE);WAIT(1);UNTIL(%GUI%="GUICHEST");
            FOR(#i,%#start%,89);
              GETSLOTITEM(53,#id,#stacky);WAIT(30ms);
              IF(%#stacky%!=0);#start=%#i%;BREAK;ENDIF;
              GETSLOTITEM(%#i%,#id,#stack);WAIT(30ms);
              IF((%#id%!=346)&&(%#id%!=287));SLOTCLICK(%#i%,l,true);ENDIF;
            NEXT;
            IF(%#stacky%!=0);
              GUI;
              #posx=%XPOS%;#posz=%ZPOS%;KEYDOWN(RIGHT);KEYDOWN(SNEAK);
              DO;WAIT(10MS);UNTIL((%XPOS%=%#posx%+1)|(%XPOS%=%#posx%-1)|(%ZPOS%=%#posz%+1)|(%ZPOS%=%#posz%-1));
              WAIT(150ms);KEYUP(SNEAK);KEYUP(RIGHT);
              #cont=0;DO;WAIT(1);INC(#cont);IF(%#cont%>3);BREAK;ENDIF;UNTIL((%HITID%=54)||(%HITID%=146));
              IF((%HITID%=54)||(%HITID%=146));
                }$$/sethome guardar$${
              ELSE;
                #ylook=%HITY%-%YPOS%;
                IF((%#ylook%<0)|(%#ylook%>2));
                  unset(baus);
                  LOG("&2[Macro] &aAcabaram os baús, apenas &2PESCANDO&a.");
                  BREAK;
                ENDIF;
                }$$/home baus$${
                DO;UNTIL((%HITID%=54)||(%HITID%=146));
                IF(%#ylook%=0);LOOK(,0);ENDIF;
                IF(%#ylook%=1);LOOK(,0);LOOK(,-30);ENDIF;
                IF(%#ylook%=2);LOOK(,0);LOOK(,-50);ENDIF;
                WAIT(1);
                #cont=0;DO;WAIT(1);INC(#cont);IF(%#cont%>3);BREAK;ENDIF;UNTIL((%HITID%=54)||(%HITID%=146));
                IF((%HITID%=54)||(%HITID%=146));
                  }$$/sethome guardar$${
                ELSE;
                  unset(baus);
                  LOG("&2[Macro] &aAcabaram os baús, apenas &2PESCANDO&a.");
                  BREAK;
                ENDIF;
              ENDIF;
            ENDIF;
          UNTIL(%#stacky%=0);
          }$$/home pescar$${
          DO;UNTIL((%XPOS%=%#xpescar%)&&(%ZPOS%=%#zpescar%));
        ENDIF;
      ENDIF;
    ENDIF;
    LOOK(,0);LOOK(,-90);
  ENDIF;
  IF((%XPOS%!=%#xpescar%)|(%ZPOS%!=%#zpescar%));
    }$$/home pescar$${
    DO;UNTIL((%XPOS%=%#xpescar%)&&(%ZPOS%=%#zpescar%));
    LOOK(,0);LOOK(,-90);
  ENDIF;
  IF(%YPOS%!=%#ypescar%);
    DO;KEYDOWN(JUMP);WAIT(300ms);KEYUP(JUMP);WAIT(2);UNTIL(%YPOS%=%#ypescar%);
    LOOK(,0);LOOK(,-90);
  ENDIF;
 
  IF(%DURABILITY%<15);
    GETSLOT(287,#u);
    IF(%#u%=-1);
      LOG("&4[Macro] &cAcabou a linha, não tem como reparar &4!!!");
      LOG("&4[Macro] &cPesca do %PLAYER% &4DESLIGADO&c.");
      UNSET(subs);GUI;STOP;
    ENDIF;
    LOOK(,90);
    DO;KEY(USE);WAIT(1);UNTIL(%DURABILITY%>15);
    GETSLOT(287,#v);
    IF(%#v%=-1);
      GUI;
      LOOK(,60);
      wait(1);
      IF((%HITID%=54)||(%HITID%=146));
        DO;KEY(USE);WAIT(1);UNTIL(%GUI%="GUICHEST");
        GETSLOT(287,#v);
        IF(%#v%!=-1);
          INC(#u,18);
          SLOTCLICK(%#v%);
          SLOTCLICK(%#u%);
        ENDIF;
        DO;PRESS(ESCAPE);WAIT(1);UNTIL(%GUI%="NONE");
      ENDIF;
    ENDIF;
    LOOK(,0);LOOK(,-90);
  ENDIF;
  IF(%GUI%="NONE");
    DO;WAIT(3);IF(%GUI%="NONE");GUI(INVENTORY);ENDIF;UNTIL(%GUI%="GUIINVENTORY");
    LOOK(,0);LOOK(,-90);
  ENDIF;
  KEY(USE);WAIT(10ms);KEY(USE);WAIT(10ms);
LOOP;
}$$
